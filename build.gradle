buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'license'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

defaultTasks 'clean', 'licenseFormat', 'check', 'build'

ext.projectName = 'Gooey'
ext.organization = 'Christopher Newport University'
ext.url = 'https://github.com/Nunnery/Gooey'
ext.inceptionYear = '2014'
ext.packaging = 'jar'
ext.description = 'An automated Swing testing framework for jUnit.'

group = 'edu.cnu.cs'
archivesBaseName = 'gooey'
version = '1.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

jar.archiveName = "gooey.jar"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Gooey'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A library to automate Swing testing.'
                url 'https://github.com/Nunnery/Gooey'

                scm {
                    connection 'scm:git:https://github.com/Nunnery/Gooey/'
                    developerConnection 'scm:git:https://github.com/Nunnery/Gooey/'
                    url 'https://github.com/Nunnery/Gooey/'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'flores'
                        name 'Roberto Flores'
                        email 'roberto.flores@cnu.edu'
                    }
                }

                contributors {
                    contributor {
                        name 'Richard Harrah'
                        email 'richard.harrah.13@cnu.edu'
                    }
                }
            }
        }
    }
}

processResources {
    // Include in final JAR
    from 'LICENSE.txt'
}

license {
    ext.name = project.name
    ext.organization = project.organization
    ext.url = project.url
    ext.year = project.inceptionYear
    header file('HEADER.txt')
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}